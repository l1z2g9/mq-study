#### Create active-standby multi-instance QM
Refer to active-standby.md to create multi-instance QM1 (earch & moon)
earch is active
moon is standby

#### Node 1 (earth)
```
$ runmqsc QM1 <<EOF
STOP LISTENER('SYSTEM.DEFAULT.LISTENER.TCP') IGNSTATE(YES)
DEFINE QLOCAL('DEV.DEAD.LETTER.QUEUE') REPLACE
ALTER QMGR DEADQ('DEV.DEAD.LETTER.QUEUE')

SET CHLAUTH('*') TYPE(ADDRESSMAP) ADDRESS('*') USERSRC(NOACCESS) DESCR('Back-stop rule - Blocks everyone') ACTION(REPLACE)
SET CHLAUTH('DEV.APP.SVRCONN') TYPE(ADDRESSMAP) ADDRESS('*') USERSRC(CHANNEL) CHCKCLNT(REQUIRED) DESCR('Allows connection via APP channel') ACTION(REPLACE)

DEFINE LISTENER('SYSTEM.LISTENER.TCP.1') TRPTYPE(TCP) PORT(1414) CONTROL(QMGR) REPLACE
START LISTENER('SYSTEM.LISTENER.TCP.1')

SET AUTHREC PRINCIPAL('app') OBJTYPE(QMGR) AUTHADD(CONNECT,INQ)
SET AUTHREC PROFILE('DEV.**') PRINCIPAL('app') OBJTYPE(QUEUE) AUTHADD(BROWSE,GET,INQ,PUT)

** Add QM to cluster
DEFINE LISTENER (TSW3CLU1.LISTENER) TRPTYPE (TCP) CONTROL (QMGR) PORT (11414) REPLACE
START LISTENER (TSW3CLU1.LISTENER)

DEFINE CHANNEL (TSW3CLU1.TO.QM1) CHLTYPE (CLUSRCVR) CONNAME ('earth(11414),moon(11414)') CLUSTER (TSW3CLU1) REPLACE
DEFINE CHANNEL (TSW3CLU1.TO.MERCURY) CHLTYPE (CLUSSDR) CONNAME ('mercury(11416)') CLUSTER (TSW3CLU1) REPLACE

** Allow all remote queue managers connect to this QM
SET CHLAUTH('*') TYPE(QMGRMAP) QMNAME(*) ADDRESS(*) MCAUSER('mqm') ACTION(REPLACE)

ALTER QMGR REPOS('TSW3CLU1') REPOSNL(' ') MAXMSGL(104857600) PERFMEV(ENABLED) DEADQ('DEV.DEAD.LETTER.QUEUE')
** ALTER QMGR DEFCLXQ(CHANNEL) // single transimission queue

** Add option DEFBIND(NOTFIXED) in cluster local queuq to allow the workload management algorithm to select the most suitable destination on a per message basis
DEFINE QLOCAL (DEV.CLUSTER_Q) CLUSTER (TSW3CLU1) DEFPSIST (YES) DEFBIND(NOTFIXED) REPLACE
SET AUTHREC PROFILE('DEV.CLUSTER_Q') PRINCIPAL('app') OBJTYPE(QUEUE) AUTHADD(BROWSE,GET,INQ,PUT,DSP)
EOF
```

#### Node 3 (mercury)
```
$ runmqsc MERCURY <<EOF
DEFINE LISTENER (TSW3CLU1.LISTENER) TRPTYPE (TCP) CONTROL (QMGR) PORT (11416) REPLACE
START LISTENER (TSW3CLU1.LISTENER)
DEFINE CHANNEL (TSW3CLU1.TO.MERCURY) CHLTYPE (CLUSRCVR) CONNAME ('mercury(11416)') CLUSTER (TSW3CLU1) REPLACE
DEFINE CHANNEL (TSW3CLU1.TO.QM1) CHLTYPE (CLUSSDR) CONNAME ('earth(11414),moon(11414)') CLUSTER (TSW3CLU1) REPLACE

SET CHLAUTH('*') TYPE(QMGRMAP) QMNAME(*) ADDRESS(*) MCAUSER('mqm') ACTION(REPLACE)

ALTER QMGR REPOS('TSW3CLU1') REPOSNL(' ') MAXMSGL(104857600) PERFMEV(ENABLED) DEADQ('DEV.DEAD.LETTER.QUEUE')
** ALTER QMGR DEFCLXQ(CHANNEL)

SET AUTHREC PROFILE('SYSTEM.CLUSTER.TRANSMIT.QUEUE') PRINCIPAL('app') OBJTYPE(QUEUE) AUTHADD(BROWSE,GET,INQ,PUT)
EOF
```


#### Node 4 (mars)
```
$ runmqsc MARS <<EOF
DEFINE LISTENER (TSW3CLU1.LISTENER) TRPTYPE (TCP) CONTROL (QMGR) PORT (11417) REPLACE
START LISTENER (TSW3CLU1.LISTENER)
DEFINE CHANNEL (TSW3CLU1.TO.MARS) CHLTYPE (CLUSRCVR) CONNAME ('mars(11417)') CLUSTER (TSW3CLU1) REPLACE
DEFINE CHANNEL (TSW3CLU1.TO.QM1) CHLTYPE (CLUSSDR) CONNAME ('earth(11414),moon(11414)') CLUSTER (TSW3CLU1) REPLACE

SET CHLAUTH('*') TYPE(QMGRMAP) QMNAME(*) ADDRESS(*) MCAUSER('mqm') ACTION(REPLACE)

DEFINE QLOCAL (DEV.CLUSTER_Q) CLUSTER (TSW3CLU1) DEFPSIST (YES) DEFBIND(NOTFIXED) REPLACE
SET AUTHREC PROFILE('DEV.CLUSTER_Q') PRINCIPAL('app') OBJTYPE(QUEUE) AUTHADD(BROWSE,GET,INQ,PUT)

SET AUTHREC PRINCIPAL('app') OBJTYPE(QMGR) AUTHADD(CONNECT,INQ)
EOF
```

#### Node 5 (venus)
```
$ runmqsc VENUS <<EOF
DEFINE LISTENER (TSW3CLU1.LISTENER) TRPTYPE (TCP) CONTROL (QMGR) PORT (11418) REPLACE
START LISTENER (TSW3CLU1.LISTENER)
DEFINE CHANNEL (TSW3CLU1.TO.VENUS) CHLTYPE (CLUSRCVR) CONNAME ('venus(11418)') CLUSTER (TSW3CLU1) REPLACE
DEFINE CHANNEL (TSW3CLU1.TO.MERCURY) CHLTYPE (CLUSSDR) CONNAME ('mercury(11416)') CLUSTER (TSW3CLU1) REPLACE

SET CHLAUTH('*') TYPE(QMGRMAP) QMNAME(*) ADDRESS(*) MCAUSER('mqm') ACTION(REPLACE)

SET AUTHREC PROFILE('SYSTEM.CLUSTER.TRANSMIT.QUEUE') PRINCIPAL('app') OBJTYPE(QUEUE) AUTHADD(BROWSE,GET,INQ,PUT)
EOF
```

##### Overview of the structure
```
earth:11414 / moon:11414 (FR) -> QM1 -> MERCURY 
mercury:11416 (FR) -> MERCURY -> QM1
mars:11417 (PR) -> MARS -> EARTH
venus:11418 (PR) -> VENUS -> MERCURY


DEV.CLUSTER_Q is hosted on both earth/moon and mars
```

##### Verify the cluster and channel on earth
```
$ runmqsc QM1
DISPLAY CLUSQMGR(*) all
DISPLAY CHSTATUS(*) all
```

##### Testing to put / get message from mercury
```
$ export MQSAMP_USER_ID=app
$ export MQSERVER='DEV.APP.SVRCONN/TCP/MERCURY(1414)'

$ /opt/mqm/samp/bin/amqsphac DEV.CLUSTER_Q MERCURY <<< "passw0rd"
```

Message should be delivered to earth/moon and mars by round-robin, the delivery of messages can be verified in their web console.  
Or issue following command to check.
```
$ /opt/mqm/samp/bin/amqsget DEV.CLUSTER_Q EARTH
$ /opt/mqm/samp/bin/amqsget DEV.CLUSTER_Q MARS
```


#### Switch over to standby QM from earth to moon
```
$ endmqm -i -s QM1

$ export MQSAMP_USER_ID=app
$ export MQSERVER='DEV.APP.SVRCONN/TCP/VENUS(1414)'
$ /opt/mqm/samp/bin/amqsphac DEV.CLUSTER_Q VENUS <<< "passw0rd"
```
